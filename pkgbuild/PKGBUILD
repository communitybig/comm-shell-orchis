# Maintainer: Tales A. Mendon√ßa <talesam@gmail.com>

pkgname=comm-shell-orchis
pkgdesc="Orchis Shell Theme Suite for BigCommunity"
pkgver=$(date +%y.%m.%d)
pkgrel=$(date +%H%M)
arch=('any')
license=('GPL3')
url="https://github.com/vinceliuice/Orchis-theme"
makedepends=('sassc' 'optipng')
depends=('gtk3' 'gtk4' 'gnome-shell')
provides=("$pkgname")
install=pkgbuild.install
source=("git+${url}.git#branch=master"
        "pkgbuild.install")
md5sums=('SKIP'
         'SKIP')

prepare() {
    cd "$srcdir/Orchis-theme"
    # Install themes in temporary directory with all required options
    ./install.sh \
        -d temp-themes \
        -t all \
        --shell \
        --libadwaita
}

package() {
    cd "$srcdir/Orchis-theme"
    
    # Create themes directory
    install -d "${pkgdir}/usr/share/themes"

    # Copy and rename Dark-Compact themes
    for theme_dir in ./temp-themes/*-Dark-Compact; do
        theme_name=$(basename "$theme_dir")
        if [[ "$theme_name" == "Orchis-Dark-Compact" ]]; then
            new_theme_name="Big-Blue"
        else
            new_theme_name="Big-${theme_name#Orchis-}"
            new_theme_name="${new_theme_name%-Dark-Compact}"
        fi
        cp -r "$theme_dir" "${pkgdir}/usr/share/themes/$new_theme_name"
    done

    # Copy and rename Light-Compact themes
    for theme_dir in ./temp-themes/*-Light-Compact; do
        theme_name=$(basename "$theme_dir")
        if [[ "$theme_name" == "Orchis-Light-Compact" ]]; then
            new_theme_name="Big-Blue-Light"
        else
            new_theme_name="Big-${theme_name#Orchis-}"
            new_theme_name="${new_theme_name%-Light-Compact}-Light"
        fi
        cp -r "$theme_dir" "${pkgdir}/usr/share/themes/$new_theme_name"
    done
}
