# Maintainer: Tales A. Mendon√ßa <talesam@gmail.com>

pkgname=comm-shell-orchis
pkgdesc="Orchis Shell Theme Suite for BigCommunity"
pkgver=$(date +%y.%m.%d)
pkgrel=$(date +%H%M)
arch=('any')
license=('GPL3')
url="https://github.com/vinceliuice/Orchis-theme"
makedepends=('sassc' 'optipng')
depends=('gtk3' 'gtk4' 'gnome-shell')
provides=("$pkgname")
source=("git+${url}.git#branch=master")
md5sums=('SKIP')

package() {
    cd "$srcdir/Orchis-theme"
    
    # Install directly to package directory with libadwaita support
    ./install.sh \
        --dest "${pkgdir}/usr/share/themes" \
        -t all \
        --shell \
        --libadwaita

    # Create necessary directories for libadwaita links
    install -dm755 "${pkgdir}/usr/share/themes/gtk-4.0"

    # Rename Dark-Compact themes to Big-* standard
    for theme_dir in "${pkgdir}/usr/share/themes"/*-Dark-Compact; do
        theme_name=$(basename "$theme_dir")
        if [[ "$theme_name" == "Orchis-Dark-Compact" ]]; then
            new_theme_name="Big-Blue"
        else
            new_theme_name="Big-${theme_name#Orchis-}"
            new_theme_name="${new_theme_name%-Dark-Compact}"
        fi
        mv "$theme_dir" "${pkgdir}/usr/share/themes/$new_theme_name"
    done

    # Rename Light-Compact themes to Big-* standard
    for theme_dir in "${pkgdir}/usr/share/themes"/*-Light-Compact; do
        theme_name=$(basename "$theme_dir")
        if [[ "$theme_name" == "Orchis-Light-Compact" ]]; then
            new_theme_name="Big-Blue-Light"
        else
            new_theme_name="Big-${theme_name#Orchis-}"
            new_theme_name="${new_theme_name%-Light-Compact}-Light"
        fi
        mv "$theme_dir" "${pkgdir}/usr/share/themes/$new_theme_name"
    done
}
